#!/bin/csh -f

# runs ELAS 

# example of a "run" script expected for each stereo algorithm in alg-XXX directory

if ($#argv != 4) then
  echo "usage: run <im0.png> <im1.png> <ndisp> <outdir>"
  exit 1
endif

# runs algorithm on image pair <im0.png>, <im1.png>, using <ndisp> disparity levels

# creates output in directory <outdir>:
# disparity maps disp0.pfm (dense), and optionally disp0_s.pfm (sparse - no interpolation of "holes")
# as well as time.txt (runtime of dense version in seconds)

set im0 = $1
set im1 = $2
set ndisp = $3
set outdir = $4

# since ELAS requires pgm files as input, convert them using imageMagick's "convert" tool:

### update 8/23/2015: use png2pgm.cpp instead to track down different
### results between linux and cygwin

set tmpim0 = tmp-im0.pgm
set tmpim1 = tmp-im1.pgm
set tmplog = tmp-log.txt

# get relative path to elas executable
set p = $0:h
if ($p == $0) set p = .
set p = $p/build

#convert $im0 $tmpim0
#convert $im1 $tmpim1
$p/../../code/png2pgm $im0 $tmpim0
$p/../../code/png2pgm $im1 $tmpim1

@ maxdisp = $ndisp - 1

# run first sparse without interpolation
(set echo; $p/elas $tmpim0 $tmpim1 $outdir/disp0_s.pfm $maxdisp 1)

# run again with interpolation, save output to log file
(set echo; $p/elas $tmpim0 $tmpim1 $outdir/disp0.pfm $maxdisp > $tmplog)
cat $tmplog

# create timing information based on second run
# extract seconds from output like "runtime: 0.96s  (0.68s/MP)"
awk -F'[ s]' '{print $2}' $tmplog > $outdir/time.txt

# remove temporary files
rm -f $tmpim0 $tmpim1 $tmplog


### NOTES:

# 1. If your algorithm produces disparities in 16-bit PGM/PNG/TIF or
# floating-point TIF format, you can use the "imgcmd" tool (part of
# cvkit, see webpage) to convert to PFMs as follows:
#
#   imgcmd <dispmap> -float $outdir/disp0.pfm
#
# This will also work for 8-bit integer disparities; note however that
# the disparity ranges exceed 255, except at quarter resolution.

# 2. If your algorithm produces some "invalid" disparities (i.e., a
# sparse disparity map), they need to be mapped to INFINITY in the PFM
# file.  You can do this as follows:
#
#   imgcmd <dispmap> -float -valid 0 $maxdisp $outdir/disp0.pfm
#
# The "-valid" option sets disparities outside 0..$maxdisp to INF.

# 3. If your disparity values are scaled by a factor <s>, use the
# option -div during the conversion:
#
#   imgcmd <dispmap> -float -div <s> -valid 0 $maxdisp $outdir/disp0.pfm

# 4. If your program does not measure its runtime itself, you can use
# the unix 'time' command to produce the timing info:
#
#   /usr/bin/time -f '%e' -o $outdir/time.txt $p/elas $tmpim0 $tmpim1 \
#                            $outdir/disp0.pfm $maxdisp
#
# Note that this will also include the time to read the imput files
# and write the pfm file, which do not need to be counted.  For most
# algorithms the difference will be minor, but for fast algorithms you
# might want to measure the runtime from withing the program.
