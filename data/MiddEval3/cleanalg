#!/bin/csh -f

# cleans results (disp*.pfm, time*.txt, and disp*-c.png files) for given algorithm

if ($#argv < 2) then
  echo ""
  echo "  usage: ./cleanalg <res> <ALG>[<n>]"
  echo ""
  echo "    <res>      - one of F, H, Q"
  echo "    <ALG>[<n>] - name of algorithm, optionally followed by suffix"
  echo "                 use 'ALG\*' to remove results for all suffixes"
  echo ""
  echo "  removes all files {test,training}<res>/*/disp<alg><n>*pfm,"
  echo "                                           disp<alg><n>*-c.png,"
  echo "                                           time<alg><n>.txt"
  echo ""
  exit 1
endif

set res = $1
set a = "$2"

if ($res != 'F' && $res != 'H' && $res != 'Q') then
  echo "unknown resolution $res"
  exit 1
endif

set t1 = test$res
set t2 = training$res

touch .tmpxxx # to avoid "no match" error
set f = (.tmpxx* $t1/*/disp0$a.pfm $t1/*/disp0${a}_s.pfm $t1/*/time$a.txt \
                 $t2/*/disp0$a.pfm $t2/*/disp0${a}_s.pfm $t2/*/time$a.txt \
                 $t1/*/disp0$a-c.png $t1/*/disp0${a}_s-c.png \
                 $t2/*/disp0$a-c.png $t2/*/disp0${a}_s-c.png)
if ($#f > 1) then
  echo ""
  echo Removing $f[2-] | tr ' ' '\n'
  echo ""
  echo -n "*** Ok to remove (y/n)? "
  set ans = $<
  if ($ans == y) then
    rm -f $f
  else
    echo not removing
  endif
endif
rm -f .tmpxxx
