#!/bin/csh -f

# run "official" evaluation using full-size GT

if ($#argv < 3) then
  echo ""
  echo '  runs "official" evaluation using full-size GT'
  echo ""
  echo "  usage: ./runevalF [-b] <res> <dataset> <thresh> [<alg1> ...]"
  echo ""
  echo "    <res>      - one of F, H, Q"
  echo "    <dataset>  - 'training'/'all' or one of the datasets, e.g. 'Piano'"
  echo "    <thresh>   - error threshold"
  echo "    <alg1> ... - name of algorithm(s) to evaluate (default: all)"
  echo ""
  echo "    option -b ('brief') outputs data with single header line for"
  echo "                        easy importing into other programs"         
  echo ""
  echo "  examples:"
  echo ""
  echo "    ./runevalF Q Piano 1.0 ELAS"
  echo "    ./runevalF H training 2.0 ELAS SGM GT"
  echo "    ./runevalF H training 1.0 ELAS_s SGM_s"
  echo "    ./runevalF -b H all 1.0"
  echo ""
  exit 1
endif

# get relative path to evaldisp executable
set p = $0:h
if ($p == $0) set p = .
set p = $p/code

set evalprog = $p/evaldisp
if (! -x $evalprog) then
  echo "executable $evalprog not found"
  exit 1
endif

@ brief = ("$1" == "-b")
if ($brief) shift

set res = $1
set ds = $2
set thresh = $3
shift
shift
shift
set algs = ($*)

if ($res != 'F' && $res != 'H' && $res != 'Q') then
  echo "unknown resolution $res"
  exit 1
endif

set traind = training$res
if (! -d $traind) then
  echo "dataset directory $traind not found"
  exit 1
endif

# use ground truth from full-size training data 
set gtd = trainingF
if (! -d $gtd) then
  echo "ground-truth directory $gtd not found"
  exit 1
endif

if ($ds == training || $ds == all) then
  set dsets = ($traind/*)
else if (-d $traind/$ds) then
  set dsets = ($traind/$ds)
else
  echo "unknown dataset $ds"
  exit 1
endif

if ($brief) then # print column headers only once
  printf "dataset       algorithm      "
  $evalprog $thresh # print column headers
endif

foreach d ($dsets)
  set g = $gtd/$d:t
  if (! -e $g/calib.txt) then
    echo "$g/calib.txt not found (download MiddEval3-data-F.zip)"
    exit 1
  endif
  if (! -e $g/disp0GT.pfm) then
    echo "$g/disp0G.pfm not found (download MiddEval3-GT0-F.zip)"
    exit 1
  endif
  set rounddisp = `awk -F= '/isint/ {print $2}' $g/calib.txt`
  if (! $brief) then  # verbose output format
    echo ====================================================== $d
    if ($rounddisp) then
      echo -n "  **rounding   "
    else
      echo -n "               "
    endif
    $evalprog $thresh # print column headers
  endif
  
  # get maxdisp from disp folder $d, not GT folder $g!
  set maxdisp = `awk -F= '/ndisp/ {print $2}' $d/calib.txt`
  foreach a ($algs)
    if (-e $d/disp0$a.pfm) then
      if ($brief) printf "%-14s" $d:t   # print dataset name in first column
      printf "%-15s" $a
      $evalprog $d/disp0$a.pfm $g/disp0GT.pfm $thresh $maxdisp $rounddisp $g/mask0nocc.png
    endif
  end
  if ($#algs == 0) then  # if no algs given, evaluate all disp0*pfm files
    foreach f ($d/disp0*pfm)
      set a = `expr $f : '.*disp0\(.*\)\.pfm'`
      if ($a == GTy) continue    # no point in evaluating these
      if ($brief) printf "%-14s" $d:t   # print dataset name in first column
      printf "%-15s" $a
      $evalprog $f $g/disp0GT.pfm $thresh $maxdisp $rounddisp $g/mask0nocc.png
    end
  endif
end
