#!/bin/csh -f

# runs algorithm on dataset(s)

if ($#argv < 3) then
  echo ""
  echo "  usage: ./runalg <res> <dataset> <ALG> [<n>]"
  echo ""
  echo "    <res>     - one of F, H, Q"
  echo "    <dataset> - 'training', 'test', 'all', or one of the datasets, e.g. 'Piano'"
  echo "    <ALG>     - name of algorithm (requires ./alg-<ALG>/run)"
  echo "    <n>       - optional suffix to append to the result names"
  echo ""
  echo "  sample usage to run ELAS algorithm on half-resolution training set:"
  echo ""
  echo "    ./runalg H training ELAS"
  echo ""
  exit 1
endif

set res = $1
set ds = $2
set alg = $3
set suffix = ''
if ($#argv > 3) set suffix = $4

if ($res != 'F' && $res != 'H' && $res != 'Q') then
  echo "unknown resolution $res"
  exit 1
endif

set testd = test$res
set traind = training$res
if (! -d $testd || ! -d $traind) then
  echo "dataset directories $testd and/or $traind not found"
  exit 1
endif

if ($ds == 'test') then
  set dsets = ($testd/*)
else if ($ds == 'training') then
  set dsets = ($traind/*)
else if ($ds == 'all') then
  set dsets = ($testd/* $traind/*)
else if (-d $testd/$ds) then
  set dsets = ($testd/$ds)
else if (-d $traind/$ds) then
  set dsets = ($traind/$ds)
else
  echo "unknown dataset $ds"
  exit 1
endif

set prog = alg-$alg/run
if (! -e $prog) then
  echo "$prog not found"
  exit 1
endif

foreach d ($dsets)
  foreach f (calib.txt im0.png im1.png)
    if (! -e $d/$f) then
      echo "file $d/$f missing"
      exit 1
    endif
  end
  set nd = `awk -F= '/ndisp/ {print $2}' $d/calib.txt`
  echo ====================== $d, ndisp=$nd
  $prog $d/im0.png $d/im1.png $nd $d
  set a = ${alg}$suffix
  if (-e $d/disp0_s.pfm) then
    mv $d/disp0_s.pfm $d/disp0${a}_s.pfm
    echo wrote $d/disp0${a}_s.pfm
  endif
  if (-e $d/disp0.pfm) then
    mv $d/disp0.pfm $d/disp0$a.pfm
    echo wrote $d/disp0$a.pfm
  else
    echo "$prog failed to create $d/disp0.pfm"
    exit 1
  endif
  if (-e $d/time.txt) then
    mv $d/time.txt $d/time$a.txt
    echo wrote $d/time$a.txt
  else
    echo "$prog failed to create $d/time.txt"
    exit 1
  endif
end
